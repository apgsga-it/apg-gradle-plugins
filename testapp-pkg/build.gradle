buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		jcenter()
	}
	dependencies {
		classpath 'com.apgsga.gradle:rpm-packager:0.3-SNAPSHOT'
		classpath 'com.apgsga.gradle:generic-publish:0.1'
		classpath 'org.hidetake:gradle-ssh-plugin:2.10.1'
		classpath 'nu.studer:gradle-credentials-plugin:1.0.7'
	}
}

apply plugin: 'com.apgsga.rpm.package'
apply plugin: 'com.apgsga.publish'
apply plugin: 'org.hidetake.ssh'
apply plugin: 'nu.studer.credentials'

project.ext {
	serviceName = project.hasProperty('serviceName') ? project.property('serviceName') : "echoservice"
	targetHost = project.hasProperty('targetHost') ? project.property('targetHost') : "jadas-e.apgsga.ch"
	sshUser = project.hasProperty('sshUser') ? project.property('sshUser') : credentials.deployUser
	sshPw = project.hasProperty('sshPw') ? project.property('sshPw') : credentials.deployUserPassword
	installTarget = project.hasProperty('installTarget') ? project.property('installTarget') : "CHEI212"
    serviceVersion = project.hasProperty('serviceVersion') ? project.property('serviceVersion') : "1.0"
	releaseNr = project.hasProperty('releaseNr') ? project.property('releaseNr') : "2"
	downloadDir = project.hasProperty('downloadDir') ? project.property('downloadDir') : "downloads"
}
println "serviceName = ${project.ext.serviceName}"
println "targetHost = ${project.ext.targetHost}"
println "installTarget = ${project.ext.installTarget}"
println "serviceVersion = ${project.ext.serviceVersion}"
println "releaseNr = ${project.ext.releaseNr}"
println "downloadDir = ${project.ext.downloadDir}"



// TODO (che, 2.10) support repositories alligned to common-repo
repositories {
	mavenLocal()
}

apgRpmPackage {
	serviceName = project.ext.serviceName
	// TODO (che,15.10) jadas-e services, neecs to be discussed how this list is maintained
	supportedServices = ["jadas", "digiflex","vkjadas", "interjadas", "interweb", project.ext.serviceName]
	dependencies = [
		"com.apgsga:testapp-service:0.1-SNAPSHOT"
	]
	resourceFilters = "serviceport"
	appConfigFilters = "general"
  	servicePropertiesDir = "resources"
	installTarget = project.ext.installTarget
	mainProgramName  = "com.apgsga.testapp.TestappApplication"
	version = project.ext.serviceVersion 
	releaseNr = project.ext.releaseNr
}

apgRpmPackage.log()

apgGenericPublishConfig {
	artefactFile =  file ("${buildDir}/distributions/${apgRpmPackage.archiveName}")
	local()
}

apgGenericPublishConfig.log()

remotes {
	dev {
	// TODO (che, 15.10) needs to be discussed, ssh policy of jenkins host
	 //knownHosts = allowAnyHosts
	  host = project.ext.targetHost
	  user = project.ext.sshUser
	  password = project.ext.sshPw
	}
}

task deployRpm {
	doLast {
		ssh.run {
		  session(remotes.dev) {
			  put from: file("${buildDir}/distributions/${apgRpmPackage.archiveName}"), into: "${project.ext.downloadDir}"
			  execute("ls -la ${project.ext.downloadDir}") { result ->
				  println result
			  }
			  executeSudo "rpm -Uvh ${project.ext.downloadDir}/${apgRpmPackage.archiveName}" , pty: true
		  }
		}
	}
}

